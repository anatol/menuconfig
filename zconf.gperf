%language=ANSI-C
%define hash-function-name kconf_id_hash
%define lookup-function-name kconf_id_lookup
%define string-pool-name kconf_id_strings
%compare-strncmp
%enum
%pic
%struct-type

struct kconf_id;

%%
mainmenu,	T_MAINMENU,	TF_COMMAND
menu,		T_MENU,		TF_COMMAND
endmenu,	T_ENDMENU,	TF_COMMAND
source,		T_SOURCE,	TF_COMMAND
choice,		T_CHOICE,	TF_COMMAND
endchoice,	T_ENDCHOICE,	TF_COMMAND
comment,	T_COMMENT,	TF_COMMAND
config,		T_CONFIG,	TF_COMMAND
menuconfig,	T_MENUCONFIG,	TF_COMMAND
help,		T_HELP,		TF_COMMAND
if,		T_IF,		TF_COMMAND|TF_PARAM
endif,		T_ENDIF,	TF_COMMAND
depends,	T_DEPENDS,	TF_COMMAND
requires,	T_REQUIRES,	TF_COMMAND
optional,	T_OPTIONAL,	TF_COMMAND
default,	T_DEFAULT,	TF_COMMAND
prompt,		T_PROMPT,	TF_COMMAND
tristate,	T_TRISTATE,	TF_COMMAND
def_tristate,	T_DEF_TRISTATE,	TF_COMMAND
bool,		T_BOOLEAN,	TF_COMMAND
boolean,	T_BOOLEAN,	TF_COMMAND
def_bool,	T_DEF_BOOLEAN,	TF_COMMAND
def_boolean,	T_DEF_BOOLEAN,	TF_COMMAND
int,		T_INT,		TF_COMMAND
hex,		T_HEX,		TF_COMMAND
string,		T_STRING,	TF_COMMAND
select,		T_SELECT,	TF_COMMAND
enable,		T_SELECT,	TF_COMMAND
range,		T_RANGE,	TF_COMMAND
on,		T_ON,		TF_PARAM
%%
